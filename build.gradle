plugins {
    id "java"
    id "jacoco"
    id "checkstyle"
    id "maven-publish"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
}

group "net.shiruka"

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencyManagement {
    imports {
        mavenBom "io.github.parentpom:ppom:3.4.45"
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://repo.nukkitx.com/maven-snapshots/"
    }
    mavenLocal()
}

javadoc {
    options.tags = ["todo"]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

checkstyle {
    configFile = file("checkstyle.xml")
    checkstyleTest.enabled = false
}

test {
    jvmArgs += ['-Duser.language=en', '-Duser.country=US']
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

dependencies {
    implementation "org.projectlombok:lombok"
    implementation "org.jetbrains:annotations"
    implementation "com.google.guava:guava"
    implementation "it.unimi.dsi:fastutil"
    implementation "org.apache.logging.log4j:log4j-core"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
    implementation "com.nukkitx.protocol:bedrock-v448:2.8.1-SNAPSHOT"

    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.jetbrains:annotations"

    testImplementation "org.llorllale:cactoos-matchers"
    testImplementation "org.hamcrest:hamcrest-library"
    testImplementation "com.google.guava:guava-testlib"
    testImplementation "org.mockito:mockito-junit-jupiter"
    testImplementation "org.junit.jupiter:junit-jupiter"
}

publishing {
    publications {
        maven(MavenPublication) { publication ->
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}
