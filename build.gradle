plugins {
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "org.jreleaser" version "0.7.0-SNAPSHOT"
    id "org.jreleaser.jdks" version "0.7.0-SNAPSHOT"
}

group "net.shiruka"

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Duser.language=en', '-Duser.country=US']
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    javadoc.options.tags = ["todo"]
    classifier "javadoc"
    from javadoc.destinationDir
}

checkstyle {
    configFile = file("checkstyle.xml")
    checkstyleTest.enabled = false
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
    mavenLocal()
}

dependencies {
    // Implementation
    implementation 'it.unimi.dsi:fastutil:8.5.4'
    implementation "com.lmax:disruptor:3.4.4"
    implementation "com.google.inject:guice:5.0.1"
    implementation "org.projectlombok:lombok:1.18.20"
    implementation "org.jetbrains:annotations:21.0.1"
    implementation "com.google.guava:guava:30.1.1-jre"
    implementation "org.apache.logging.log4j:log4j-core:2.14.1"
    implementation "com.github.Infumia.InfumiaLib:InfumiaCommon:2.3.6"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.12.4"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.4"

    // Processor
    annotationProcessor "org.projectlombok:lombok:1.18.20"
    annotationProcessor "org.jetbrains:annotations:21.0.1"

    // Test
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.2"
}

publishing {
    publications {
        pub(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version

            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
        }
    }
}

jreleaser {
    project {
        website = 'https://shiruka.github.io/'
        authors = ['Shiru ka']
        license = 'MIT'
        extraProperties.put('inceptionYear', '2021')
    }

    release {
        github {
            owner = "Shiru ka"
            overwrite = true
        }
    }
}
