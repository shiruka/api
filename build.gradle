plugins {
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "signing"
}

group "io.github.shiruka"

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

tasks.withType(JavaCompiler) {
    options.encoding = "UTF-8"
    options.compilerArgs += ["-Duser.language=en", "-Duser.country=US"]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    javadoc.options.tags = ["todo"]
    javadoc.options.addBooleanOption("html5", true)
    classifier "javadoc"
    from javadoc.destinationDir
}

checkstyle {
    configFile = file("checkstyle.xml")
    checkstyleTest.enabled = false
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
    mavenLocal()
}

dependencies {
    // Implementation
    implementation 'it.unimi.dsi:fastutil:8.5.4'
    implementation "com.lmax:disruptor:3.4.4"
    implementation "com.google.inject:guice:5.0.1"
    implementation "org.projectlombok:lombok:1.18.20"
    implementation "org.jetbrains:annotations:21.0.1"
    implementation "com.google.guava:guava:30.1.1-jre"
    implementation "org.apache.logging.log4j:log4j-core:2.14.1"
    implementation "com.github.Infumia.InfumiaLib:InfumiaCommon:2.3.8"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.12.4"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.4"

    // Processor
    annotationProcessor "org.projectlombok:lombok:1.18.20"
    annotationProcessor "org.jetbrains:annotations:21.0.1"

    // Test
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.2"
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version

            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
            pom {
                name = "Shiru ka Api"
                description = "An Api project that helps developers to create unique plugins for Shiru ka."
                url = "https://shiruka.github.io/"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://mit-license.org/license.txt"
                    }
                }
                developers {
                    developer {
                        id = "portlek"
                        name = "Hasan Demirta≈ü"
                        email = "utsukushihito@outlook.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/shiruka/api.git"
                    developerConnection = "scm:git:ssh://github.com/shiruka/api.git"
                    url = "https://github.com/shiruka/api"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
